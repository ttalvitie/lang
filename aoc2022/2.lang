@score1 = 0;
@score2 = 0;

runWhileTrue({
    @line = inputLine();
    @lineLen = strLen(line);
    ifElse(lineLen, {
        if(lineLen != 3, {
            fail("Unexpected line length.")
        });

        @opponentPlay = get8(line) - charNum("A");
        @myPlay = get8(line + 2) - charNum("X");

        if(opponentPlay >= 3 || myPlay >= 3, {
            fail("Unexpected input data")
        });

        score1 = score1 + (myPlay + 1) + (myPlay - opponentPlay + 3 + 1) % 3 * 3;

        myPlay = (opponentPlay + myPlay + 2) % 3;
        score2 = score2 + (myPlay + 1) + (myPlay - opponentPlay + 3 + 1) % 3 * 3;

        1
    }, {
        // Empty line -> end of input.
        0
    })
});

printL("%u", score1);
printL("%u", score2);

0
