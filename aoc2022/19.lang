@hashtable = HashTable.init(22, { 0 }, { 0 });

@processBlueprint = [roundCount, orePerOreRobot, orePerClayRobot, orePerObsidianRobot, clayPerObsidianRobot, orePerGeodeRobot, obsidianPerGeodeRobot]{
    @State.ore = [state]{ state };
    @State.clay = [state]{ state + 4 };
    @State.obsidian = [state]{ state + 8 };
    @State.oreRobots = [state]{ state + 12 };
    @State.clayRobots = [state]{ state + 16 };
    @State.obsidianRobots = [state]{ state + 20 };
    @State.geodeRobots = [state]{ state + 24 };
    @State.roundsDone = [state]{ state + 28 };
    @State.sizeof = 32;
    @State.init = {
        @ret = brk;
        brk = brk + State.sizeof;
        ret
    };

    @initState = State.init();
    *State.ore(initState) = 0;
    *State.clay(initState) = 0;
    *State.obsidian(initState) = 0;
    *State.oreRobots(initState) = 1;
    *State.clayRobots(initState) = 0;
    *State.obsidianRobots(initState) = 0;
    *State.geodeRobots(initState) = 0;
    *State.roundsDone(initState) = 0;

    @roundDist = shiftLeft(1, 26);
    assert(roundCount < 64);

    @isEnd = [state]{
        *State.roundsDone(state) == roundCount
    };

    // Temporary states used in neighbors.
    @stateAfter = State.init();
    @stateTmp = State.init();

    @neighbors = [state, callback]{
        copyMem(state, stateAfter, State.sizeof);

        *State.ore(stateAfter) = *State.ore(stateAfter) + *State.oreRobots(stateAfter);
        *State.clay(stateAfter) = *State.clay(stateAfter) + *State.clayRobots(stateAfter);
        *State.obsidian(stateAfter) = *State.obsidian(stateAfter) + *State.obsidianRobots(stateAfter);
        *State.roundsDone(stateAfter) = *State.roundsDone(stateAfter) + 1;

        @dist = roundDist - *State.geodeRobots(state);

        if(*State.ore(state) >= orePerOreRobot, {
            copyMem(stateAfter, stateTmp, State.sizeof);
            *State.ore(stateTmp) = *State.ore(stateTmp) - orePerOreRobot;
            *State.oreRobots(stateTmp) = *State.oreRobots(stateTmp) + 1;
            if(callback(stateTmp, dist), {
                stateTmp = State.init()
            })
        });

        if(*State.ore(state) >= orePerClayRobot, {
            copyMem(stateAfter, stateTmp, State.sizeof);
            *State.ore(stateTmp) = *State.ore(stateTmp) - orePerClayRobot;
            *State.clayRobots(stateTmp) = *State.clayRobots(stateTmp) + 1;
            if(callback(stateTmp, dist), {
                stateTmp = State.init()
            })
        });

        if(*State.ore(state) >= orePerObsidianRobot && *State.clay(state) >= clayPerObsidianRobot, {
            copyMem(stateAfter, stateTmp, State.sizeof);
            *State.ore(stateTmp) = *State.ore(stateTmp) - orePerObsidianRobot;
            *State.clay(stateTmp) = *State.clay(stateTmp) - clayPerObsidianRobot;
            *State.obsidianRobots(stateTmp) = *State.obsidianRobots(stateTmp) + 1;
            if(callback(stateTmp, dist), {
                stateTmp = State.init()
            })
        });

        if(*State.ore(state) >= orePerGeodeRobot && *State.obsidian(state) >= obsidianPerGeodeRobot, {
            copyMem(stateAfter, stateTmp, State.sizeof);
            *State.ore(stateTmp) = *State.ore(stateTmp) - orePerGeodeRobot;
            *State.obsidian(stateTmp) = *State.obsidian(stateTmp) - obsidianPerGeodeRobot;
            *State.geodeRobots(stateTmp) = *State.geodeRobots(stateTmp) + 1;
            if(callback(stateTmp, dist), {
                stateTmp = State.init()
            })
        });

        if(callback(stateAfter, dist), {
            stateAfter = State.init()
        })
    };
    @heuristic = [state]{
        @ret = 0;

        @ore = *State.ore(state);
        @clay = *State.clay(state);
        @obsidian = *State.obsidian(state);
        @oreRobots = *State.oreRobots(state);
        @clayRobots = *State.clayRobots(state);
        @obsidianRobots = *State.obsidianRobots(state);
        @geodeRobots = *State.geodeRobots(state);
        @origOreRobots = oreRobots;
        @origClayRobots = clayRobots;
        @origObsidianRobots = obsidianRobots;
        @origGeodeRobots = geodeRobots;

        forRange(*State.roundsDone(state), roundCount, [i]{
            @buyOreRobot = ore >= (oreRobots - origOreRobots + 1) * orePerOreRobot;
            @buyClayRobot = ore >= (clayRobots - origClayRobots + 1) * orePerClayRobot;
            @buyObsidianRobot = ore >= (obsidianRobots - origObsidianRobots + 1) * orePerObsidianRobot && clay >= (obsidianRobots - origObsidianRobots + 1) * clayPerObsidianRobot;
            @buyGeodeRobot = ore >= (geodeRobots - origGeodeRobots + 1) * orePerGeodeRobot && obsidian >= (geodeRobots - origGeodeRobots + 1) * obsidianPerGeodeRobot;

            ore = ore + oreRobots;
            clay = clay + clayRobots;
            obsidian = obsidian + obsidianRobots;
            ret = ret + roundDist - geodeRobots;

            oreRobots = oreRobots + buyOreRobot;
            clayRobots = clayRobots + buyClayRobot;
            obsidianRobots = obsidianRobots + buyObsidianRobot;
            geodeRobots = geodeRobots + buyGeodeRobot
        });
        ret
    };
    @nodeHash = [state]{
        @hash = 0;
        forRange(0, 8, [i]{
            hash = combineHash(hash, hashU32(*(state + 4 * i)))
        });
        hash
    };
    @nodeEq = [state1, state2]{
        memEqual(state1, state2, State.sizeof)
    };

    roundCount * roundDist - aStar(initState, hashtable, isEnd, neighbors, heuristic, nodeHash, nodeEq)
};

@ret1 = 0;
@ret2 = 1;

runWhileTrue({
    @line = inputLine();
    ifElse0(get8(line), {
        @blueprintId = 0;
        @orePerOreRobot = 0;
        @orePerClayRobot = 0;
        @orePerObsidianRobot = 0;
        @clayPerObsidianRobot = 0;
        @orePerGeodeRobot = 0;
        @obsidianPerGeodeRobot = 0;

        parseStr(line, "Blueprint %u: Each ore robot costs %u ore. Each clay robot costs %u ore. Each obsidian robot costs %u ore and %u clay. Each geode robot costs %u ore and %u obsidian.", &blueprintId, &orePerOreRobot, &orePerClayRobot, &orePerObsidianRobot, &clayPerObsidianRobot, &orePerGeodeRobot, &obsidianPerGeodeRobot);

        @geodes = processBlueprint(24, orePerOreRobot, orePerClayRobot, orePerObsidianRobot, clayPerObsidianRobot, orePerGeodeRobot, obsidianPerGeodeRobot);
        ret1 = ret1 + blueprintId * geodes;

        if(blueprintId <= 3, {
            @geodes2 = processBlueprint(32, orePerOreRobot, orePerClayRobot, orePerObsidianRobot, clayPerObsidianRobot, orePerGeodeRobot, obsidianPerGeodeRobot);
            ret2 = ret2 * geodes2
        });

        1
    })
});

printL("%u", ret1);
printL("%u", ret2);

0
